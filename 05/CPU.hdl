// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM == 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to restart the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // Address of next instruction

    PARTS:
    // Use built-in ARegister, DRegister and PC chips

    // Updating the A register
    Not(in=instruction[15], out=ains);
    Mux16(a=aluout, b=instruction, sel=ains, out=aregin);
    Or(a=ains, b=instruction[5], out=aregload);
    ARegister(in=aregin, load=aregload, out=aregout, out[0..14]=addressM);

    // Updating the D register
    And(a=instruction[15], b=instruction[4], out=dregload);
    DRegister(in=aluout, load=dregload, out=dregout);

    // Running ALU
    Mux16(a=aregout, b=inM, sel=instruction[12], out=aluin);
    ALU(x=dregout, y=aluin, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluout, out=outM, zr=zr, ng=ng);

    // Write in RAM or not
    And(a=instruction[15], b=instruction[3], out=writeM);

    // Program counter = jump logic
    Not(in=zr, out=notzr);
    Not(in=ng, out=notng);
    And(a=notzr, b=notng, out=pos);
    Not(in=pos, out=notpos);
    DMux8Way(in=instruction[15], sel=instruction[0..2], a=false, b=jgt, c=jeq, d=jge, e=jlt, f=jne, g=jle, h=jmp);
    And(a=jgt, b=pos, out=jgtout);
    And(a=jeq, b=zr, out=jeqout);
    And(a=jge, b=notng, out=jgeout);
    And(a=jlt, b=ng, out=jltout);
    And(a=jne, b=notzr, out=jneout);
    And(a=jle, b=notpos, out=jleout);
    Or8Way(in[0]=jgtout, in[1]=jeqout, in[2]=jgeout, in[3]=jltout, in[4]=jneout, in[5]=jleout, in[6]=jmp, in[7]=false, out=jump);
    PC(in=aregout, load=jump, inc=true, reset=reset, out[0..14]=pc);
}
